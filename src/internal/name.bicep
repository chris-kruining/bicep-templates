import { ResourceType, Abbreviation, Location } from '../types.bicep'

@export()
func create_name(context object, template string) string =>
  toLower(reduce(items(context), template, (t, e) => replace(string(t), '$${e.key}', e.value)))

@export()
func to_resource_abbreviation(resourceType ResourceType) Abbreviation =>
  {
    resourceGroup: 'rg'
    containerApp: 'ca'
    containerAppEnvironment: 'cae'
    keyVault: 'kv'
    managedIdentity: 'id'
    staticWebApp: 'stapp'
    appService: 'app'
    appServicePlan: 'asp'
    containerRegistry: 'cr'
    appConfiguration: 'appcs'
    serviceBus: 'sb'
    serviceBusQueue: 'sbq'
    serviceBusTopic: 'sbt'
    serviceBusTopicSubscription: 'sbts'
    applicationInsights: 'appi'
    storageAccount: 'st'
    logAnalytics: 'la'
  }[resourceType]

@export()
func to_location_abbreviation(location Location) string =>
  {
    southafricanorth: 'san'
    southafricawest: 'saw'
    australiacentral: 'auc'
    australiacentral2: 'auc2'
    australiaeast: 'aue'
    australiasoutheast: 'ause'
    centralindia: ''
    eastasia: ''
    japaneast: 'jpe'
    japanwest: 'jpw'
    jioindiacentral: ''
    jioindiawest: ''
    koreacentral: 'krc'
    koreasouth: 'krs'
    southindia: ''
    southeastasia: ''
    westindia: ''
    canadacentral: 'cac'
    canadaeast: 'cae'
    francecentral: 'frc'
    francesouth: 'frs'
    germanynorth: 'den'
    germanywestcentral: 'dewc'
    italynorth: 'itn'
    northeurope: 'eun'
    norwayeast: 'noe'
    norwaywest: 'now'
    polandcentral: 'poc'
    spaincentral: 'spc'
    swedencentral: 'swc'
    switzerlandnorth: 'szn'
    switzerlandwest: 'szw'
    uksouth: 'uks'
    ukwest: 'ukw'
    westeurope: 'euw'
    mexicocentral: 'mxc'
    israelcentral: ''
    qatarcentral: 'qtc'
    uaecentral: ''
    uaenorth: ''
    brazilsouth: 'brs'
    brazilsoutheast: 'brse'
    brazilus: ''
    centralus: 'usc'
    centraluseuap: ''
    eastus: 'use'
    eastus2: 'use2'
    eastus2euap: ''
    eastusstg: ''
    northcentralus: 'usnc'
    southcentralus: 'ussc'
    southcentralusstg: ''
    westcentralus: 'uswc'
    westus: 'usw'
    westus2: 'usw2'
    westus3: 'usw3'
  }[location]
